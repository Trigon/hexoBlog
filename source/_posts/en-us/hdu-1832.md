---
title: HDU-1832
lang: en-us
'lang:': en-us
date:  2013-06-25 21:53:40
tags:
- 线段树
- Pascal
- HighSchool
categories: ACM/OI
---
# Luck and Love
Problem link:
[HDU-1832](http://acm.hdu.edu.cn/search.php?field=problem&key=HDOJ+2007+Summer+Exercise%A3%A83%A3%A9-+Hold+by+Wiskey&source=1&searchmode=source)

# Analyze
This problem is a simple problem using segment tree. In some ways, we need to changed the single node of the tree and get the maxnum of a range<!--more-->
It is easy to write two dimension segment tree. The memory use is the square of the one-dimension. The time using for changing and asking are also the square of the one-dimension's.
The outer tree do not need to update. Only the small insider tree in the node of outer tree need to be updated.
The cost of  changing a node is `Log n` and there are total `Log n` nodes.

*The input datas may have `l` larger than `r` which need to be taken consideration.*

# Code
{% codeblock HDU1832 lang:pascal %}
var
  c:char;
  t1,t2:double;
  n,i,x,x1,x2,y,y1,y2,z,ans,tmp:longint;
  lx,ly,rx,ry:array[0..5005] of longint;
  f:array[0..500,0..5000] of longint;
procedure make(h,k,y1,y2:longint);
  var
    mid:longint;
  begin
    ly[k]:=y1;ry[k]:=y2;
    f[h,k]:=-1;
    if y1=y2 then exit;
    mid:=(y1+y2) div 2;
    make(h,k*2,y1,mid);
    make(h,k*2+1,mid+1,y2);
  end;
procedure build(k,x1,x2,y1,y2:longint);
  var
    mid:longint;
  begin
    lx[k]:=x1;rx[k]:=x2;
    make(k,1,y1,y2);
    if x1=x2 then exit;
    mid:=(x1+x2) div 2;
    build(k*2,x1,mid,y1,y2);
    build(k*2+1,mid+1,x2,y1,y2);
  end;
function max(a,b:longint):longint;
  begin
    if a<b then max:=b else max:=a;
  end;
procedure update(h,k,y:longint);
  var
    mid:longint;
  begin
    f[h,k]:=max(f[h*2,k],f[h*2+1,k]);
    if ly[k]=ry[k] then exit;
    mid:=(ly[k]+ry[k]) div 2;
    if y<=mid then update(h,k*2,y) else update(h,k*2+1,y);
  end;
procedure insert(h,k,y,z:longint);
  var
    l,r,mid:longint;
  begin
    l:=ly[k];r:=ry[k];
    if z>f[h,k] then begin f[h,k]:=z;;end;
    if (l=r) then exit;
    mid:=(l+r) div 2;
    if y<=mid then insert(h,k*2,y,z) else insert(h,k*2+1,y,z);
  end;   
procedure insertx(k,x,y,z:longint);
  var
    l,r,mid:longint;
  begin
    l:=lx[k];r:=rx[k];
    if (l=r) then begin  insert(k,1,y,z);exit;end;
    mid:=(l+r) div 2;
    if x<=mid then insertx(k*2,x,y,z) else insertx(k*2+1,x,y,z);
    update(k,1,y);
  end;
function find(h,k,y1,y2:longint):longint;
  var
    l,r,mid,t1,t2:longint;
  begin
    l:=ly[k];r:=ry[k];
    if (y1<=l) and (y2>=r) then begin find:=f[h,k];exit;end;
    mid:=(l+r) div 2;
    if y1<=mid then t1:=find(h,k*2,y1,y2) else t1:=-1;
    if y2> mid then t2:=find(h,k*2+1,y1,y2) else t2:=-1;
    if t2>t1 then t1:=t2;
    find:=t1;
  end;
function findx(k,x1,x2,y1,y2:longint):longint;
  var
    l,r,mid,t1,t2:longint;
  begin
    l:=lx[k];r:=rx[k];
    if (x1<=l) and (x2>=r) then begin findx:=find(k,1,y1,y2);exit;end;
    mid:=(l+r) div 2;
    if x1<=mid then t1:=findx(k*2,x1,x2,y1,y2) else t1:=-1;
    if x2> mid then t2:=findx(k*2+1,x1,x2,y1,y2) else t2:=-1;
    if t2>t1 then t1:=t2;
    findx:=t1;
  end;
begin
  readln(n);
  while n<>0 do begin 
  fillchar(lx,sizeof(lx),0);
  fillchar(rx,sizeof(rx),0);
  fillchar(ly,sizeof(ly),0);
  fillchar(ry,sizeof(ry),0);
  fillchar(f,sizeof(f),0);
  build(1,100,200,0,1000);
  for i:= 1 to n do 
    begin
      read(c);
      if c='I' then
        begin
          read(x);readln(t1,t2);
          t1:=t1+0.00001;t2:=t2+0.00001;
          y:=trunc(t1*10);z:=trunc(t2*10);
          insertx(1,x,y,z);
        end
      else
        begin
          read(x1,x2);readln(t1,t2);
          t1:=t1+0.00001;t2:=t2+0.00001;
          y1:=trunc(t1*10);y2:=trunc(t2*10);
          if x1>x2 then begin tmp:=x1;x1:=x2;x2:=tmp;end;
          if y1>y2 then begin tmp:=y1;y1:=y2;y2:=tmp;end;
          ans:=findx(1,x1,x2,y1,y2);
          if ans=-1 then writeln(ans) else 
            begin
              write(ans div 10,'.');
              writeln(ans mod 10);
            end;
        end;
    end;readln(n);end;
end.
{% endcodeblock %}